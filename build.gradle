plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

group = 'com.jvav.script'
version = '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }

    configurations {
        include
        implementation.extendsFrom(include)
    }

    dependencies {
        implementation 'org.openjdk.nashorn:nashorn-core:15.4'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }
}

subprojects {
    plugins.apply("kotlin")

    group = parent.group
    version = parent.version
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    include project(":Core")
    include project(":Interrupter")
    include project(":IO")
    include project(":Reflect")
}

test {
    useJUnitPlatform()
}

jar.manifest {
    attributes "Main-Class": "com.jvav.Main"
}

shadowJar {
    configurations = [project.configurations.include]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}
